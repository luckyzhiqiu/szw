//经营:征兵获取兵力
<server,sessionID,game,netMsg>
methodName=netMsg[0];//方法名
userID=netMsg[1];//用户ID
tick=netMsg[2];//通行证

if(!game.checkTick(userID,tick))//通行证校验失败
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",0)
	);
	exit();
}

//修改行数据
user=game.rd.updateRow
(
	game.cnn,"user","id",userID,"*",
	//修改回调
	##<user,game>
		@link methodName,sessionID;
		json=json_decode(user.json);
		makeSoldier=ref(json.makeSoldier);//经营:征兵
		beginTime=ref(makeSoldier.beginTime);//资源增长开始时间戳（毫秒）
		dt=ref(makeSoldier.dt);//每轮间隔时间（秒）
		dt1000=dt*1000;
		recvSign=0;
		newBeginTime=beginTime;
		curTime=time_milli();//当前时间
		row=game.table.level.getRowFromColName("level",user.level);//任务数据
		maxRecvCount=number(row.getValFromColName("ERP3max"));//招募士兵累积
		roundBeginTime=beginTime-dt1000*makeSoldier.recvCount;//第1轮开始时间
		roundEndTime=roundBeginTime+dt1000;//第1轮结束时间
		if(makeSoldier.recvCount>0)//有储存的收成轮数
		{
			recvSign=1;
			endTime=beginTime+dt1000*(maxRecvCount-makeSoldier.recvCount);
			if(curTime<endTime)//未超时
			{
				--makeSoldier.recvCount;//减少次数
			}
			else//已超时
			{
				makeSoldier.recvCount=maxRecvCount-1;
				newBeginTime=curTime;
			}
		}
		else
		{
			recvCount=floor((curTime-beginTime)/dt1000);//未储存的收成轮数
			recvCount=limit(recvCount,0,maxRecvCount);
			if(recvCount>0)//有未储存的收成轮数
			{
				recvSign=1;
				makeSoldier.recvCount=recvCount-1;//减少次数
				if(recvCount<maxRecvCount)
				{
					remainingTime=curTime-beginTime;//剩下的时间
					newCurTime=curTime-remainingTime+recvCount*dt1000;
					newBeginTime=newCurTime;
				}
				else
				{
					newBeginTime=curTime;
				}
			}
		}
		if(recvSign)//有收成
		{
			heroArr=json.hero;
			charm=number(user.charm);//势力魅力
			//收成
			point=charm;//势力收益
			pointPlus=0;//豪杰上阵收益加成
			//上阵加成
			useHeroArr=makeSoldier.hero;
			c=size(useHeroArr);
			numMgr=game.numMgr;
			i=0;while(i<c)
			{
				info=useHeroArr[i];
				heroIndex=info.heroIndex;
				if(heroIndex>=0)
				{
					heroBeginTime=info.beginTime;
					if(heroBeginTime<roundEndTime)
					{
						heroBeginTime=limit(heroBeginTime,roundBeginTime,roundEndTime);
						useTime=roundEndTime-heroBeginTime;//当前轮的上阵时间
						usePercent=useTime/dt1000;
						heroInfo=heroArr[heroIndex];
						pointPlus+=floor(heroInfo.charmTotal*usePercent);
					}
				}
				++i;
			}
			
			//各种万分比加成
			percent=1;
			//红颜势力万分比加成
			percent+=json.wifePlus.businessPercent;
			//豪杰势力万分比加成
			percent+=numMgr.getHeroRecruitPercent(heroArr);
			//权贵势力万分比加成
			unionID=int(user.unionID);
			richmanIDArr=numMgr.getRichmanIDArr(unionID);//已拉拢权贵ID数组
			percent+=numMgr.getRichmanAddPercent(richmanIDArr,type=3);
			
			//加成
			point*=percent;
			
			totalPoint=floor(point)+pointPlus;//总收益
			
			food=number(user.food);
			if(totalPoint>food)
			{
				//回复
				game.sendMsg
				(
					sessionID,
					arr(methodName+"Result",-3)//粮草不足
				);
				return(0);//不修改
			}
			soldier=number(user.soldier);
			
			//修改dt
			if(charm<10000)
			{
				dt=60;
			}
			else if(charm<300000)
			{
				dt=floor(3*charm/500);
			}
			else
			{
				dt=1800;
			}

			//保存
			user.soldier=soldier+totalPoint;
			user.food=food-totalPoint;
			beginTime=newBeginTime;
			
			makeSoldierCount=ref(json.dailyTask.makeSoldierCount);
			makeSoldierCount+=1;//日常任务累计次数
			
			makeSoldierAchievement=ref(json.achievement.makeSoldier);
			makeSoldierAchievement+=1;//成就累计//经营农业次数
			
			limitTask=json.limitTask;
			activityMgr=game.activityMgr;
			activityMgr.updateLimitTask(limitTask);//更新限时任务数据
			limitTask.makeSoldierCount+=1;//限时任务//征兵次数
			
			//编码
			user.json=json_encode(json);
			//回复
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",1,makeSoldier,soldier,food,point,pointPlus,makeSoldierCount,makeSoldierAchievement,limitTask.makeSoldierCount)
			);
			return(1);//修改
		}
		else//没收成
		{
			//回复
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",-2)
			);
			return(0);//不修改
		}
	##.,
	//修改完成回调
	##<row,userData>
		//...
	##.,
	//解锁后回调
	##<row,userData>
		//...
	##.,
	game,//自定义数据userData
	1000//加锁时间（毫秒），例如：1000
);
if(empty(user))//用户不存在
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",-1)
	);
}
