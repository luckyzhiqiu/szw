//目标权贵
<server,sessionID,game,netMsg>
methodName=netMsg[0];//方法名
userID=netMsg[1];//用户ID
tick=netMsg[2];//通行证
targetRichmanIDS=int(netMsg[3]);//目标权贵ID

if(!game.checkTick(userID,tick))//通行证校验失败
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",0)
	);
	exit();
}

user=game.rd.getRow(game.cnn,"user","id",userID,"*",1000);
unionID=user.unionID;
if(unionID==0)//未加入联盟
{
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",-3)
	);
	exit();
}

unionData=game.rd.updateRow
(
	game.cnn,"union","id",unionID,"*",
	//修改回调
	##<unionData,game>
		@link sessionID,methodName,userID,targetRichmanIDS;
		leaderUserID=number(unionData.leaderUserID);
		unionJson=json_decode(unionData.json);
		viceLeaderUserIDMap=unionJson.viceLeaderUserIDMap;
		if(exist(viceLeaderUserIDMap,userID) || leaderUserID==userID)
		{
			targetRichmanID=number(unionData.targetRichmanID);
			//保存
			if(targetRichmanID==0)
			{
				unionData.targetRichmanID=targetRichmanIDS;
			}
			else
			{
				if(targetRichmanID!=targetRichmanIDS)
				{
					unionData.targetRichmanID=targetRichmanIDS;
				}
				else
				{
					unionData.targetRichmanID=0;
					targetRichmanIDS=0;
				}
			}	

			//回复
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",1,targetRichmanIDS)
			);
			return(1);//修改
		}
		else//无权限修改
		{
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",-2)
			);
			return(0);
		}
	##.,
	//修改完成回调
	##<row,userData>
		//...
	##.,
	//解锁后回调
	##<row,userData>
		//...
	##.,
	game,//自定义数据userData
	1000//加锁时间（毫秒），例如：1000
);
if(empty(unionData))//联盟不存在
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",-1)
	);
}