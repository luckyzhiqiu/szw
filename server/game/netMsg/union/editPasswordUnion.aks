//修改联盟密码
<server,sessionID,game,netMsg>
methodName=netMsg[0];//方法名
userID=netMsg[1];//用户ID
tick=netMsg[2];//通行证
passwordVerify=netMsg[3];//密码验证

if(!game.checkTick(userID,tick))//通行证校验失败
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",0)
	);
	exit();
}

user=game.rd.getRow(game.cnn,"user","id",userID,"*",1000);
unionID=number(user.unionID);
if(unionID==0)//未加入联盟
{
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",-2)
	);
	exit();
}

unionData=game.rd.updateRow
(
	game.cnn,"union","id",unionID,"*",
	//修改回调
	##<unionData,game>
		@link sessionID,methodName,userID,passwordVerify;
		leaderUserID=number(unionData.leaderUserID);
		if(leaderUserID!=userID)//不是盟主
		{
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",-3)
			);
			return(0);
		}
		
		password=unionData.password;
		if(password!="")//密码不为空不能修改
		{
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",-4)
			);
			return(0);
		}
		
		//保存
		unionData.password=passwordVerify;

		//回复
		game.sendMsg
		(
			sessionID,
			arr(methodName+"Result",1)
		);
		return(1);
	##.,
	//修改完成回调
	##<row,userData>
		//...
	##.,
	//解锁后回调
	##<row,userData>
		//...
	##.,
	game,//自定义数据userData
	1000//加锁时间（毫秒），例如：1000
);
if(empty(unionData))//联盟不存在
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",-1)
	);
}