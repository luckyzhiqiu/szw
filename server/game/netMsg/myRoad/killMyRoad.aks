//击杀
<server,sessionID,game,netMsg>
methodName=netMsg[0];//方法名
userID=netMsg[1];//用户ID
tick=netMsg[2];//通行证
heroIndex=netMsg[3];//豪杰位置

if(!game.checkTick(userID,tick))//通行证校验失败
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",0)
	);
	exit();
}

//修改行数据
user=game.rd.updateRow
(
	game.cnn,"user","id",userID,"*",
	//修改回调
	##<user,game>
		@link methodName,sessionID,heroIndex;
		genTime=user.genTime;
		genTimeS=strtotime(genTime);//创建时间戳
		nowTime=strtotime(now());//现在时间戳
		passTimeDay=8;//经过多少天
		passSecond=60*60*24*passTimeDay;//经过多少秒

		if((nowTime-passSecond)>genTimeS)//时间已过
		{
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",-2)
			);
			return(0);
		}

		json=json_decode(user.json);
		myRoad=json.myRoad;
		myRoadId=myRoad.id;
		heroIndexMap=myRoad.heroIndexMap;
		
		//判断豪杰是否存在
		heroArr=json.hero;
		if(!exist(heroArr,heroIndex))//豪杰不存在
		{
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",-3)
			);
			return(0);//不修改
		}
		
		if(exist(heroIndexMap,heroIndex))//豪杰已使用
		{
			game.sendMsg
			(
				sessionID,
				arr(methodName+"Result",-4)
			);
			return(0);//不修改
		}
		
		tableData=game.table;
		myRoadTable=tableData.myRoad;
		
		numMgr=game.numMgr;
		hero=heroArr[heroIndex];//对战豪杰
		//战斗力=武力资质×5000×等级＋武力属性
		attack=numMgr.getHeroAttack(hero,json);
		
		bossHP=myRoad.bossHP;
		bossHP-=attack;
		if(bossHP<=0 && myRoadId<10)//战斗未结束
		{
			myRoadId+=1;
			rowM=myRoadTable.getRowFromColName("id",myRoadId+1);
			if(empty(rowM))//已达到最大关卡
			{
				myRoad.bossHP=0;
				myRoad.id=myRoadId;
				heroIndexMap[heroIndex]=1;
				user.json=json_encode(json);
				
				game.sendMsg
				(
					sessionID,
					arr(methodName+"Result",2,myRoad)
				);
				return(1);//修改
			}
			else
			{
				life=number(rowM.getValFromColName("life"));//血量
				bossHP=life;
			}
		}
					
		//保存
		myRoad.id=myRoadId;
		myRoad.bossHP=bossHP;
		heroIndexMap[heroIndex]=1;
		user.json=json_encode(json);
		//回复
		game.sendMsg
		(
			sessionID,
			arr(methodName+"Result",1,myRoad)
		);
		
		return(1);//修改
	##.,
	//修改完成回调
	##<row,userData>
		//...
	##.,
	//解锁后回调
	##<row,userData>
		//...
	##.,
	game,//自定义数据userData
	1000//加锁时间（毫秒），例如：1000
);
if(empty(user))//用户不存在
{
	//回复
	game.sendMsg
	(
		sessionID,
		arr(methodName+"Result",-1)
	);
}
